(console.log "sibilant from compiled file")
(include "./macro")
(include "./common")
(include "./universal")
(include "./miccup")

(const electron (require 'electron)
       fs (require 'fs)
       path (require 'path)
       sibilant (require 'sibilant))

(set window 'Datastore (require "nedb"))
(var DB (new Datastore {filename: "store.db"
                        autoload: true
                        timestampData: true })
     ;;  currently not used
     db {_store: DB
         load: (#>
                (DB.findOne
                 {url: (get-webview-url)}
                 (#(err, doc)
                   (console.log
                    "loaded" doc)
                   (when doc
                         (get doc 'click-history)))))
         save: (#>
                (DB.insert {url: (get-webview-url)
                            'click-history: [] }))
         }
     jsyaml (require "js-yaml"))

;; utility
(def set-webview-url ()
     ;; arguments is NOT an array!!!
     (var arglist (Array.prototype.slice.call arguments)
          wv-idx (if (= 1 (length arglist))
                     0
                     (first arglist))
          url (if (= 1 (length arglist))
                  (first arglist)
                  (last arglist)))
     (|> (getWebview wv-idx)
         (.setAttribute "src" url)))

(def get-webview-url (wv-idx)
     (|> (getWebview wv-idx)
         (.getAttribute "src")))

(def style2object (style)
     ;; does not have .name and .value when passed a style object
     (var rtn {})
     (each (i) (range style.length)
           (var key (get style i)
                val (get style key))
           (when (!= val "initial")
                 (set rtn key val)))
     rtn)

(def attr2object (attr)
     ;; has .name and .value when passed a DOM element.attributes object;
     (var rtn {})
     (each (i) (range attr.length)
           (var key (get attr i "name")
                val (get attr key "value"))
           (set rtn key val))
     rtn)

(def resolve-asset-dir (dirname)
     (|> (path.join global.__dirname dirname)
         (path.resolve)))

;; yaml based data control
(var jsyaml (require "js-yaml")
     __session-dir-path (resolve-asset-dir "session")
     __handler-dir-path (resolve-asset-dir "handler")
     __plugin-dir-path (resolve-asset-dir (get Conf "PLUGIN_DIR_NAME")))

;; housekeeping
(when (not (fs.existsSync __session-dir-path))
      (fs.mkdirSync __session-dir-path))

(var control-state {session-list: (m-prop [])
                    handler-mapping: (m-prop {})
                    plugin-mapping: (m-prop {})
                    active-session: (m-prop)})

(var WebViewDriver { exec: (#(channel arg)
                             (var func (get WebViewDriver channel))
                             (when func (func arg))) })
(set WebViewDriver Const.WEBVIEW_ELEMENT_CLICK
     (#(js)
       (var asess (control-state.active-session))
       (if (not asess)
           (alert "no active session")
           (do
            (var locater (JSON.parse js))
            (asess.history.push locater)
            (set asess 'is-dirty true)
            (control-state.active-session asess)))))

(set window 'WebViewDriver WebViewDriver)

(def load-all-sessions! ()
     (fs.readdir
      __session-dir-path
      (#(err file-list)
        (var re-yml (regex "yml$"))
        (control-state.session-list
         (|> (or file-list [])
             (.filter (#(fname)
                        (fname.match re-yml)))
             (.map (#(fname)
                     (|> (path.join __session-dir-path fname)
                         (fs.readFileSync "utf-8")
                         (jsyaml.safeLoad)
                         (tap (set 'filename fname)))))))
        (m.redraw))))

(def make-plugin-object (desc init)
     {init: init
      description: desc})

(def compile-sibilant-plugin! (plugin-sibilant-path)
     (|> plugin-sibilant-path
         (fs.readFileSync "utf-8")
         (sibilant.sibilize)
         (fs.writeFileSync
          (path.join
           (path.dirname plugin-sibilant-path)
           "index.js")
          #)))

(def load-all-plugins! ()
     (when (not (fs.existsSync __plugin-dir-path))
           (fs.mkdirSync __plugin-dir-path))
     
     (fs.readdir
      __plugin-dir-path
      (#(err file-list)
        (var out {})
        (each (plugin-name i) (or file-list [])
              (var spl (plugin-name.split ".")
                   get-plugin-file-relpath (#(plugin-file)
                                             (path.join
                                              __plugin-dir-path
                                              plugin-name
                                              plugin-file))
                   maybe-sib-file (|> ["index.sib"
                                       "index.sibilant"]
                                      (.map get-plugin-file-relpath)
                                      (.filter fs.existsSync)
                                      (first))
                   start-plugin! (#() (alert
                                       (+ "[OVERRIDE] if you are seeing this message, "
                                          "the plugin was NOT loaded properly"))))

              (when maybe-sib-file
                    ;; this will likely stop working in the future
                    (|> maybe-sib-file
                        (fs.readFileSync "utf-8")
                        (sibilant.sibilize)
                        (eval))
                    ;; after eval, we expect to have these in the namespace:
                    ;; - plugin-info
                    ;; - start-plugin!()
                    (set out plugin-name
                         (make-plugin-object
                          (get plugin-info 'description)
                          start-plugin!))
                    ;; if the eval fails, investigate this method
                    ;; (compile-sibilant-plugin! maybe-sib-file)
                    )

              (if (get out plugin-name)
                  (console.warn (+ "already loaded: " plugin-name))
                  
                  (fs.existsSync (get-plugin-file-relpath "index.js"))
                  (do
                   (var plugin-module
                        (require (+ "../"
                                    (path.join
                                     (get Conf "PLUGIN_DIR_NAME")
                                     plugin-name))))
                   (set out plugin-name
                        (make-plugin-object
                         (get plugin-module 'info 'description)
                         (get plugin-module 'start))))))
        (control-state.plugin-mapping out)
        (m.redraw))))

(def make-session (url name)
     {url: url
      name: name
      history: []
      })
(var ui-state {selected-tab: (m-prop 'plugin-list)
               scratch-view: (m-prop)})

(def render-session-list (session-list)
     (var asess (control-state.active-session)
          td-style {border: "1px solid gray"
                    padding: "0.5em" }
          ;; STRUCTURE: [key, [header?], [renderer?]]
          ;; - key
          ;; - use 2nd as header if present
          ;; - use last as renderer if a function
          hdr-list [['history
                     "# actions"
                     (#(hist)
                       (+ ""
                          (get hist 'length)
                          " actions"))]
                    ['name]
                    ['url]
                    ['filename]
                    ])
     ["table"
      {style: {border-collapse: "collapse"
               border: "1px solid black"
               width: "100%"
               padding: "0.5em"}
       }
      ["tr"
       (mapcar (#(hdr)
                 ["th" (if (typeof (= "string" (second hdr)))
                           (second hdr)
                           (first hdr))])
               hdr-list)]
      (mapcar (#(sess)
                ["tr"
                 {style: {border: "1px solid gray"
                          background: (if (= sess.filename
                                             (maybe-get asess 'filename))
                                          "yellow"
                                          "")}
                  onclick: (#>
                            (control-state.active-session sess)
                            (ui-state.selected-tab 'action-list))
                  }
                 (mapcar
                  (#(hdr)
                    (var key (first hdr)
                         maybe-func (last hdr)
                         renderer (if (= "function" (typeof maybe-func))
                                      maybe-func
                                      identity))
                    ["td"
                     {style: td-style }
                     (renderer (get sess key))])
                  hdr-list)])
              session-list)])

(def action-list-item (locater i)
     ["li"
      ["div"
       (scoped
        ;; delete button
        (var asess (control-state.active-session)
             cur-hist asess.history)
        ["button"
         {style: {margin: "0.2em"
                  width: "1.3em"
                  height: "1.3em"
                  lineHeight: "1.3em"
                  color: "white"
                  align: "center"
                  textAlign: "center"
                  verticalAlign: "middle"
                  borderRadius: "3px"
                  backgroundColor: "red" }
          onclick: (#>
                    (var head (cur-hist.slice 0 i)
                         tail (cur-hist.slice (+ 1 i)))
                    (set asess 'history (head.concat tail))
                    (set asess 'is-dirty true)
                    (control-state.active-session asess)) }
         ["i" {class: "fa fa-times" } ]])
       ["button"
        {onclick: (#>
                   (var exec-string
                        (if (or (= locater.tag "button")
                                (= locater.tag "a")
                                (and (= locater.tag "input")
                                     (or (= locater.type "submit")
                                         (= locater.type "button"))))
                            (+ "common.clickSelector('" locater.selector "')")
                            
                            true
                            (+ "document.querySelector('"
                               locater.selector
                               "').scrollIntoView({behavior: \"smooth\"})")
                            
                            null
                            ;; (+ "common.fireEventOn('" locater.selector "', 'Click')")
                            ))
                   (when exec-string
                         (|> (getWebview)
                             (execJs
                              exec-string)))
                   ) }
        (JSON.stringify locater)]]])

(def action-list-view ()
     (var sess (control-state.active-session))
     (if sess
         ["div"
          (if sess.name
              ["h1" sess.name]
              "")
          ["hr"]
          ["a"
           {style: {margin: "0.2em"
                    padding: "0.1em"
                    border: "1px solid blue"
                    background: "lightblue"
                    border-radius: "4px"
                    cursor: "pointer"}
            onclick: (#> (set-webview-url sess.url))}
           sess.url]
          ["hr"]
          ["ol"
           {style: {margin: "0.5em"
                    padding: "0.5em"}}
           (mapcar action-list-item sess.history)
           (when (get sess 'is-dirty)
                 [["hr"]
                  ["button"
                   {onclick: (#>
                              (delete sess.is-dirty)
                              (var new-session-list [])
                              (each (iter-sess) (control-state.session-list)
                                    (new-session-list.push
                                     (if (= iter-sess.filename
                                            sess.filename)
                                         sess
                                         iter-sess)))
                              (control-state.session-list new-session-list)
                              (control-state.active-session sess)
                              (var filepath (path.join __session-dir-path sess.filename))
                              (fs.writeFile
                               filepath
                               (jsyaml.safeDump sess)
                               (#(err)
                                 (if err
                                     (alert (+ "file write failed!\n"
                                               (.toString err)))
                                     (console.log (+ "wrote: " filepath)))))
                              )}
                   "save!"]])]]))

(def plugin-list-view ()
     (var plugin-mapping (control-state.plugin-mapping)
          control-element (document.getElementById "panel-A")
          view-element (document.getElementById "panel-B"))
     ["div"
      ["table"
       {style: {margin: "0.5em"
                padding: "0.5em"}}
       ["tbody"
        ["tr"
         ["th" "name"]
         ["th" "description"]]
        (|> plugin-mapping
            (Object.keys)
            (mapcar (#(k)
                      (var initialize! (get plugin-mapping k 'init))
                      ["tr"
                       ["td"
                        ["button"
                         {onclick: (#>
                                    (initialize!
                                     control-element view-element)
                                    (webviewControl.attachWebviewEventListeners 0))}
                         k]]
                       ["td" (get plugin-mapping k 'description)]])
                    #))]]])

(var ui {controller: (#>
                      (console.info "in controller")
                      {})
         tab-def: {saved-urls: {label: "saved urls"

                                ;; find out nicer way to DRY
                                bare-state: {name: null
                                             url: null}
                                state: (m-prop {name: null
                                                url: null})
                                render: (#>
                                         (var self this)
                                         [ ["b" "new session"]
                                          ["hr"]
                                          ["div"
                                           ["div"
                                            ["label"
                                             ["input"
                                              {type: "text"
                                               placeholder: "url"
                                               size: 40
                                               onchange: (#(evt)
                                                           (var cur-state (self.state))
                                                           (set cur-state 'url evt.target.value)
                                                           (self.state cur-state)
                                                           )} ]
                                             "session url" ]]
                                           ["div"
                                            ["label"
                                             ["input"
                                              {type: "text"
                                               placeholder: "name"
                                               size: 40
                                               onchange: (#(evt)
                                                           (var cur-state (self.state))
                                                           (set cur-state 'name evt.target.value)
                                                           (self.state cur-state)
                                                           )}]
                                             "session name (optional)"]]
                                           ["div"
                                            ["button"
                                             {onclick: (#>
                                                        (var lstate (self.state)
                                                             url (get lstate 'url)
                                                             name (get lstate 'name))
                                                        (when (and url name)
                                                              (var new-session (make-session url name))
                                                              (set new-session 'filename
                                                                   (|> (new Date)
                                                                       (.toISOString)
                                                                       (.substring 0 10)
                                                                       (+ "_"
                                                                          (.replace (get new-session 'url)
                                                                                    (regex "^(.+://)")
                                                                                    "")
                                                                          ".yml")))
                                                              (self.state self.bare-state)
                                                              (|> (control-state.session-list)
                                                                  (.push new-session))
                                                              (control-state.active-session
                                                               new-session))
                                                        )}
                                             "start new session"]]
                                           ]
                                          ["hr"]
                                          ["b" "sessions:"]
                                          ["hr"]
                                          (render-session-list
                                           (control-state.session-list)
                                           )]
                                         )
                                }
                   action-list: {label: "action list"
                                 render: action-list-view}

                   plugin-list: {label: "plugin list"
                                 render: plugin-list-view}

                   scratch: {label: "scratch"
                             render: (#>
                                      (m-miccup (ui-state.scratch-view)))}}
         view: (#(ctrl)
                 (var self this ;; self.view is the view object here, for example
                      table (|> (m-miccup
                                 ["div"
                                  {style: {position: "relative"
                                           float: "left"
                                           background: "#444"
                                           color: "#fff"
                                           padding: "2px"
                                           margin: "2px"
                                           width: "100%"
                                           height: "100%"
                                           } }
                                  ["style"
                                   "ul.tab-list { list-style-type: none }"
                                   "ul.tab-list li { float: left; border: 1px solid red; margin: 2px; padding: 2px; width: 100px; }"
                                   "ul.tab-list li:hover { background: gray; color: black; }"
                                   "ul.tab-list li.active { background: white; color: black; }"
                                   ]
                                  (scoped
                                   ["ul.tab-list"
                                    {}
                                    (mapcar (#(k i)
                                              [(+ "li"
                                                  (if (= k (ui-state.selected-tab))
                                                      ".active"
                                                      ""))
                                               {onclick: (#>
                                                          (ui-state.selected-tab k)) }
                                               (get self.tab-def k 'label)])
                                            (Object.keys self.tab-def))
                                    ])
                                  ["div" {style: {clear: "both" } }]
                                  ["div"
                                   {style: {width: "100%"
                                            height: "100%"
                                            color: "black"
                                            background: "white"
                                            }}
                                   ;; note function call
                                   ((|> (ui-state.selected-tab)
                                        (get self.tab-def #)
                                        (get 'render)))
                                   ]
                                  ]))))})

(def setup-control-container! (element)
     (m.mount element ui)
     (load-all-sessions!)
     (load-all-plugins!))

(set exports
     'setupControlContainer setup-control-container!
     'db db)
