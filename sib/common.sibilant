;; common between host window and hosted webviews

(var Const { "WEBVIEW_ELEMENT_CLICK": "webview-element-click" })

(var require-CssSelectorGenerator (require "css-selector-generator")
     CssSelectorGenerator require-CssSelectorGenerator.CssSelectorGenerator
     require-electron (require "electron")
     ipcRenderer require-electron.ipcRenderer)

(var exports {})
(set module 'exports exports)

(def flash-element (el duration interval)
     (var overlay (document.createElement "div")
          el-bbox (el.getBoundingClientRect)
          border-size 2
          padding-size 2
          x (- el-bbox.left padding-size)
          y (- el-bbox.top padding-size)
          w (+ (- el-bbox.right el-bbox.left)
               (* 2 padding-size))
          h (+ (- el-bbox.bottom el-bbox.top)
               (* 2 padding-size)))
     (|> overlay
         (.setAttribute "style" (+ "position:absolute;"
                                   "z-index:9999;"
                                   "left:" x "px;"
                                   "top:" y "px;"
                                   "width:" w "px;"
                                   "height:" h "px;"
                                   "outline:" "blue" " dashed " border-size "px;"
                                   "background: #ccf;"
                                   "opacity: 0.5;"
                                   "filter: alpha(opacity=0.5);")))
     (document.body.appendChild overlay)
     (var hl-outline "#f00 solid 4px"
          orig-outline el.style.outline
          t0 (new Date)
          toggle-counter 0)

     (var tid (setInterval
               (#>
                (if (< duration (- (new Date) t0))
                    (scoped
                     (clearInterval tid)
                     (set el.style "outline" orig-outline)
                     (document.body.removeChild overlay))
                    (scoped
                     (set el.style "outline"
                          (if (= 0 (mod toggle-counter 2))
                              orig-outline
                              hl-outline))))
                (incr toggle-counter))
               interval)))

(set exports
     'Const Const
     'do_alert (#(x) (alert x))
     'click_selector (#(selector)
                       (var el (document.querySelector selector))
                       (when (and el el.click)
                             (flash-element el 500 100)
                             (setTimeout
                              (#>
                               (console.log
                                "CLICKY..." (el.click)))
                              3000)))
     ;; see http://stackoverflow.com/a/809182
     'fire_event_on (#(selector etype)
                      (var el (document.querySelector selector))
                      (if el.fireEvent
                          (el.fireEvent (+ "on" etype))
                          (scoped
                           (|> (document.createEvent "Events")
                               (tap (.initEvent etype true false))
                               (el.dispatchEvent)))))
     'proc_right_click (#(evt)
                         (var csg (new CssSelectorGenerator)
                              tgt evt.target)
                         (flash-element tgt 600 100)
                         (ipcRenderer.sendToHost
                          Const.WEBVIEW_ELEMENT_CLICK
                          (JSON.stringify
                           { x: evt.clientX
                             y: evt.clientY
                             tag: (evt.target.tagName.toLowerCase)
                             type: (evt.target.type.toLowerCase)
                             selector: (csg.getSelector tgt)}))))
